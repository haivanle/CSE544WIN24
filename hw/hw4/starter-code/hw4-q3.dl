/************ data model **************/
.symbol_type PersonType
.symbol_type DescriptionType

.decl person(name:PersonType, description:DescriptionType)
.input person(filename="DATA/person.facts")

.decl female(name:PersonType)
.input female(filename="DATA/female.facts")

.decl male(name:PersonType)
.input male(filename="DATA/male.facts")

.decl parent_child(p1:PersonType, p2:PersonType)
.input parent_child(filename="DATA/parent_child.facts")

.decl person_living(p:PersonType)
.input person_living(filename="DATA/person_living.facts")

/************* problem 0 **************/
/**** Find all children of Priscilla ****/
.decl p0(x:PersonType, d:DescriptionType)
// NOTE: if you want to redirect the output to a file
// you can use the syntax:
// .output p0(filename="hw2-q3-0.ans")
.output p0(IO=stdout)
p0(x,d) :- parent_child("Priscilla",x), person(x,d).

/************* problem 1 **************/
/**** Find all descendants of Priscilla and their descriptions****/
.decl p1(x:PersonType,d:DescriptionType)
.output p1(filename="../submission/hw4-q3-1.ans")
.output p1(IO=stdout)

p1(x,d) :- p0(x,d).
p1(x,d) :- p1(y,_), parent_child(y,x), person(x,d).

/************* problem 2 **************/
/**** Find the woman/women with the largest number of children and the man/men with the largest number of children. 
For each individual, you should return the name of that individual, his/her description, and the number of children.****/
.decl p2(x:PersonType, d:DescriptionType, m:number)
.decl m2(x:PersonType, d:DescriptionType, m:number)
.decl m3(x:PersonType, d:DescriptionType, m:number)
.decl largest_men(m:number)
.decl largest_women(m:number)
.output p2(filename="../submission/hw4-q3-2.ans")
.output p2(IO=stdout)

m2(x,d,m) :- male(x), person(x,d), m = count : {parent_child(x,_)}.
largest_men(m) :- m = max n : {m2(_,_,n)}.
m3(x,d,m) :- female(x), person(x,d), m = count : {parent_child(x,_)}.
largest_women(m) :- m = max n : {m3(_,_,n)}.
p2(x,d,m) :- m3(x,d,m), largest_women(m).
p2(x,d,m) :- m2(x,d,m), largest_men(m).

/************* problem 3 **************/
/**** For each person x, we call a "complete lineage" any sequence x0=x, x1, x2, â€¦ , xn where each person is the parent of the previous person, 
and the last person has no parents; the length of the sequence is n.  If x has a complete lineage of length n, 
then we also say that "x is in generation n".  Compute the minimum and maximum generation of each living person x.****/
.decl p3(x:PersonType, m1:number, m2:number)
.decl p3_temp(x:PersonType, m1:number, m2:number)
.decl gen(x:PersonType, m:number)
.output p3(filename="../submission/hw4-q3-3.ans")
.output p3(IO=stdout)

gen(x, 0) :- person(x,_), !parent_child(_,x).
gen(x, m+1) :- gen(y,m), parent_child(y,x).
p3_temp(x,m1,m2) :- person(x,_), m1 = min m : {gen(x,m)}, m2 = max m : {gen(x,m)}.
p3(x,m1,m2) :- p3_temp(x,m1,m2), person_living(x).

/************* problem 4 **************/
/**** Compute all matrilineal common ancestors, MCA.****/
.decl p4(x:PersonType)
.decl mother_line(x:PersonType, y:PersonType)
.decl son_number(x:PersonType, y:number)\
.decl number_living(x:number)
.output p4(filename="../submission/hw4-q3-4.ans")
.output p4(IO=stdout)

mother_line(x,y) :- person_living(x), female(y), parent_child(y,x).
mother_line(x,y) :- mother_line(x,z), female(y), parent_child(y,z).
son_number(x,y) :- mother_line(_,x), y = count : {mother_line(_,x)}.
number_living(m) :- m = count : {person_living(_)}.
p4(x) :- son_number(x,m), number_living(m).

/************* problem 4 **************/
/**** Find the mitochondrial Eve.****/
.decl p5(x:PersonType)
.decl Eve(x:PersonType, y:number)
.decl min_g(x:number)
.output p5(filename="../submission/hw4-q3-5.ans")
.output p5(IO=stdout)

Eve(x,m1) :- p4(x), p3_temp(x,m1,_).
min_g(y) :- y = min m1 : {Eve(_,m1)}.
p5(x) :- Eve(x,y), min_g(y).